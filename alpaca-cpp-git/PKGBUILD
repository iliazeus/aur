# Maintainer: Ilia Pozdnyakov <ilia.pozdnyakov@ya.ru>
pkgname="alpaca-cpp-git"
pkgver=r124.6041736
pkgrel=2
pkgdesc="Locally run an Instruction-Tuned Chat-Style LLM"
arch=("any")
url="https://github.com/antimatter15/alpaca.cpp"
license=("MIT")
depends=("aria2")
makedepends=("git" "make" "gcc")
conflicts=()
source=(
  "$pkgname"::"git+https://github.com/antimatter15/alpaca.cpp"
  "model-path.patch"
  "fix-colors.patch"
  "alpaca-fetch-model.sh"
)
md5sums=(
  "SKIP"
  "50a6013f2e914c592a909e9f5a0c1c50"
  "39b7bd2f971194c5bb5d4529d79f1a8f"
  "04cc071a9f678df7a279f90d09e58b9b"
)

prepare() {
  cd "$srcdir/$pkgname"
  patch --forward --strip 1 --input ../model-path.patch
  patch --forward --strip 1 --input ../fix-colors.patch
}

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  make chat
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm0755 chat "$pkgdir/usr/bin/alpaca"
  install -Dm0755 ../alpaca-fetch-model.sh "$pkgdir/usr/bin/alpaca-fetch-model"
}
